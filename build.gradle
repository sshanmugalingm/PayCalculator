buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {

    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: "jacoco"

group 'au.com.senthur'
version '1.0-SNAPSHOT'

mainClassName = 'au.com.payroll.PayrollBootstrap'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    compile 'org.springframework:spring-context:4.3.8.RELEASE'
    compile 'org.springframework:spring-orm:4.3.8.RELEASE'
    compile 'org.springframework.data:spring-data-jpa:1.11.3.RELEASE'
    compile 'org.hibernate:hibernate-core:5.2.10.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.2.10.Final'

    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'

    runtime group: 'hsqldb', name: 'hsqldb', version: '1.8.0.7'

    testCompile 'org.springframework:spring-test:4.3.8.RELEASE'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-4'
}

jar {
    baseName = 'Payroll'
    version = version
}


sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationtest/groovy')
        }
        resources.srcDir file('src/integrationtest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen {false}
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

run{
    standardInput = System.in
}

task runJar(type: JavaExec) {
    classpath = files("build/libs/${jar.baseName}-${jar.version}.jar")
    classpath += sourceSets.main.runtimeClasspath
    main = mainClassName
    standardInput = System.in
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}